buildscript {
    ext {
        springBootVersion = '3.1.5'
        jacksonModuleKotlinVersion = '2.16.0'
        tokenSupportVersion = '3.2.0'
    }
}

plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id "io.spring.dependency-management" version "1.1.4"
    id "com.vanniktech.dependency.graph.generator" version "0.8.0"
}

apply from: "buildSrc/ep-module.gradle"

group = 'no.nav.eessi.pensjon'

dependencies {

    // Spring-boot BOM-fil spesifiserer versjoner for Spring Boot-avhengigheter
    implementation(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.yaml:snakeyaml:2.2") // Pga https://security.snyk.io/package/maven/org.yaml:snakeyaml
    implementation("org.springframework.boot:spring-boot-starter-aop")
    implementation("org.springframework.boot:spring-boot-starter-cache")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-actuator")
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude module: 'junit'
        exclude module: "mockito-core"
        exclude module: 'junit-vintage-engine'
    }

    // Spring Framework BOM-fil overstyrer så vi får siste patch-versjon av Spring Framework
    implementation(platform("org.springframework:spring-framework-bom:6.1.0"))

    // Kotlin
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonModuleKotlinVersion}")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonModuleKotlinVersion}")

    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    testImplementation(platform("org.junit:junit-bom:5.10.1"))
    testImplementation("org.junit.jupiter:junit-jupiter")

    testImplementation("io.mockk:mockk:1.13.8")

    // Forenklere mocking av beans, benyttes som mockito mockbean
    testImplementation("com.ninja-squad:springmockk:4.0.2")

    // Logging
    implementation("net.logstash.logback:logstash-logback-encoder:7.4")
    configurations { all*.exclude group: 'commons-logging', module: 'commons-logging' }
    implementation group: 'org.slf4j', name: 'jcl-over-slf4j'

    // OIDC
    implementation("no.nav.security:token-validation-spring:${tokenSupportVersion}")
    implementation("no.nav.security:token-validation-jaxrs:${tokenSupportVersion}")
    implementation("no.nav.security:token-client-spring:${tokenSupportVersion}")
    // Only used for starting up locally
    implementation("no.nav.security:token-validation-test-support:2.0.5")
    testImplementation("org.pac4j:spring-webmvc-pac4j:7.0.0")

    implementation("no.nav.eessi.pensjon:ep-metrics:2.0.44")
    implementation("no.nav.eessi.pensjon:ep-logging:2.1.38")
    implementation("no.nav.eessi.pensjon:ep-routing:2.0.36"){
        exclude module: 'ep-eux'
        exclude module: 'ep-metrics'
        exclude module: 'ep-logging'
        exclude module: 'ep-personoppslag'
    }
    implementation("no.nav.eessi.pensjon:ep-security-sts:1.0.91") {
        exclude module: 'ep-metrics'
    }
    implementation("no.nav.eessi.pensjon:ep-eux:2.1.86"){
        exclude module: 'ep-metrics'
    }
}
